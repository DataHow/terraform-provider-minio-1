---
layout: ""
page_title: "Provider: Minio"
description: |-
  Provider for the Minio object storage. Allows managing buckets, policies, groups and users.
---

# Minio Provider

This provider allows managing [Minio](https://min.io) servers.

Minio is an open-source Amazon S3 compatible object storage solution.

The provider supports managing:
* Buckets
* Policies
* Groups
* Users

This provider is open-source and can be found on Github: https://github.com/foundational-solutions/terraform-provider-minio

Please report any issues or feature requests there.

## Example Usage

```terraform
terraform {
  required_providers {
    minio = {
      # ATTENTION: use the current version here!
      version = "0.1.0-alpha5"
      source  = "foundational-solutions/minio"
    }
  }
}

provider "minio" {
  # The Minio server endpoint.
  # NOTE: do NOT add an http:// or https:// prefix!
  # Set the `ssl = true/false` setting instead.
  endpoint = "localhost:9000"
  # Specify your minio user access key here.
  access_key = "00000000"
  # Specify your minio user secret key here.
  secret_key = "00000000"
  # If true, the server will be contacted via https://
  ssl = false
}

# Create a bucket.
resource "minio_bucket" "bucket" {
  name = "bucket"
}

# Create a policy.
resource "minio_canned_policy" "policy1" {
  name = "policy1"
  policy = <<EOT
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": [
        "s3:GetObject"
      ],
      "Effect": "Allow",
      "Resource": [
        "arn:aws:s3:::my-bucketname/*"
      ]
    }
  ]
}
EOT
}

# Create a user group and assign the specified policies.
resource "minio_group" "group1" {
  name = "group1"
  policies = [minio_canned_policy.policy1.name]
}

resource "minio_group" "group2" {
  name = "group2"
}


# Import an existing policy.
# (the consoleAdmin policy is created by Minio automatically)
data "minio_canned_policy" "console_admin" {
  name = "consoleAdmin"
}

# Create a user with specified access credentials, policies and group membership.
resource "minio_user" "user1" {
  access_key = "00000001"
  secret_key = "00000001"
  policies = [
    minio_canned_policy.policy1.name,
    # Note: using a data source here!
    data.minio_canned_policy.console_admin.name,
  ]
  groups = [
    minio_group.group2.name,
  ]
}
```

## Developer

Developed by [foundational](https://foundational.cc).

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- **access_key** (String, Sensitive) The access key (username).
Should be the minio root user or a user with sufficient permissions.
- **endpoint** (String) The Minio server domain.
Must not include http[s]://!
Eg: my-minio.domain.com
- **secret_key** (String, Sensitive) The secret key (password).
Should be the minio root user or a user with sufficient permissions.

### Optional

- **ssl** (Boolean) If true, https:// will be used.
